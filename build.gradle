plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.5.20'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'cyou.untitled.redisbsg'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    flatDir {
        dirs 'libs'
    }
}

sourceSets {
    all {
        languageSettings.useExperimentalAnnotation('kotlin.RequiresOptIn')
    }
}

shadowJar {
    dependencies {
        exclude(dependency('org.jetbrains.kotlin.*:.*:.*'))
    }

    archiveBaseName.set('Redis-BSG')
    archiveVersion.set(project.version as String)
    zip64 true
    relocate('org.bstats', 'cyou.untitled.redisbsg.bstats')
}

dependencies {
    // Library loading is delegated to BungeeCord
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib"
    compileOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1'
    compileOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.5.1'
    compileOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-reactive:1.5.1'
    compileOnly 'net.md-5:bungeecord-api:1.17-R0.1-SNAPSHOT'
    compileOnly name: 'BungeeSafeguard-3.1-SNAPSHOT-all'
    // Redis
    compileOnly 'io.lettuce:lettuce-core:6.1.4.RELEASE'
    // bStats
    implementation 'org.bstats:bstats-bungeecord:2.2.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}